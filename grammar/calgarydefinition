START -> REPTSTART0 .

ADDOP -> plus .
ADDOP -> minus .
ADDOP -> or .

APARAMS -> EXPR REPTAPARAMS1 .
APARAMS -> .

APARAMSTAIL -> comma EXPR .

ARITHEXPR -> TERM RIGHTRECARITHEXPR .

ARRAYSIZE -> lsqbr ARRAYSIZETAIL .

ARRAYSIZETAIL -> intnum rsqbr.
ARRAYSIZETAIL -> rsqbr .

ASSIGNOP -> assign .

ASSIGNEND -> ASSIGNOP EXPR semi .

CLASSDECL -> class id OPTCLASSDECL2 lcurbr REPTCLASSDECL4 rcurbr semi .

CLASSDECLORFUNCDEF -> CLASSDECL .
CLASSDECLORFUNCDEF -> FUNCDEF .

CONTINUATION -> REPTIDNEST id FACTORTAIL .

EXPR -> FACTOR RIGHTRECTERM RIGHTRECARITHEXPR EXPRTAIL .

EXPRTAIL -> .
EXPRTAIL -> RELOP ARITHEXPR .

FACTOR -> id FACTORIDTAIL .
FACTOR -> intnum .
FACTOR -> floatnum .
FACTOR -> lpar ARITHEXPR rpar .
FACTOR -> not FACTOR .
FACTOR -> SIGN FACTOR .

FACTORTAIL -> REPTINDICE .
FACTORTAIL -> PARAMS .

FACTORIDTAIL -> INDICE REPTINDICE CONTINUATION .
FACTORIDTAIL -> .
FACTORIDTAIL -> CONTINUATION .
FACTORIDTAIL -> PARAMS CONTINUATION .

FPARAMS -> id colon TYPE REPTARRAYSIZE REPTFPARAMS4 .
FPARAMS -> .

FPARAMSTAIL -> comma id colon TYPE REPTARRAYSIZE .

REPTFPARAMS4 -> FPARAMSTAIL REPTFPARAMS4  .
REPTFPARAMS4 -> .

FUNCBODY -> lcurbr REPTFUNCBODY1 rcurbr .

FUNCDEF -> FUNCHEAD FUNCBODY .

FUNCHEAD -> function FUNCHEADTAIL .

FUNCHEADTAIL -> id FUNCHEADTAILTAIL .

FUNCHEADTAILTAIL -> sr FUNCHEADTAILTAILTAIL .
FUNCHEADTAILTAIL -> lpar FPARAMS rpar arrow RETURNTYPE .

FUNCHEADTAILTAILTAIL -> id lpar FPARAMS rpar arrow RETURNTYPE .
FUNCHEADTAILTAILTAIL -> constructor lpar FPARAMS rpar .

INDICE -> lsqbr ARITHEXPR rsqbr .

LOCALVARDECL -> localvar id colon TYPE LOCALVARDECLTAIL .

LOCALVARDECLTAIL -> REPTARRAYSIZE semi .
LOCALVARDECLTAIL -> ENDPARAMS .

LOCALVARDECLORSTMT -> LOCALVARDECL .
LOCALVARDECLORSTMT -> STATEMENT .

MEMBERDECL -> MEMBERFUNCDECL .
MEMBERDECL -> MEMBERVARDECL .

MEMBERFUNCDECL -> function id colon lpar FPARAMS rpar arrow RETURNTYPE semi .
MEMBERFUNCDECL -> constructor colon lpar FPARAMS rpar semi .

MEMBERVARDECL -> attribute id colon TYPE REPTARRAYSIZE semi .

MULTOP -> mult .
MULTOP -> div .
MULTOP -> and .

OPTCLASSDECL2 -> isa id REPTOPTCLASSDECL22 .
OPTCLASSDECL2 -> .

PARAMS -> lpar APARAMS rpar .

RELEXPR -> ARITHEXPR RELOP ARITHEXPR .

RELOP -> eq .
RELOP -> neq .
RELOP -> lt .
RELOP -> gt .
RELOP -> leq .
RELOP -> geq .

REPTAPARAMS1 -> APARAMSTAIL REPTAPARAMS1 .
REPTAPARAMS1 -> .

REPTCLASSDECL4 -> VISIBILITY MEMBERDECL REPTCLASSDECL4 .
REPTCLASSDECL4 -> .

REPTARRAYSIZE -> ARRAYSIZE REPTARRAYSIZE .
REPTARRAYSIZE -> .

REPTFUNCBODY1 -> LOCALVARDECLORSTMT REPTFUNCBODY1 .
REPTFUNCBODY1 -> .

REPTIDNEST -> dot id IDNESTTAIL REPTIDNEST .
REPTIDNEST -> .

REPTOPTCLASSDECL22 -> comma id REPTOPTCLASSDECL22 .
REPTOPTCLASSDECL22 -> .

REPTSTART0 -> CLASSDECLORFUNCDEF REPTSTART0 .
REPTSTART0 -> .

REPTSTATBLOCK1 -> STATEMENT REPTSTATBLOCK1 .
REPTSTATBLOCK1 -> .

RETURNTYPE -> TYPE .
RETURNTYPE -> void .

RIGHTRECARITHEXPR -> .
RIGHTRECARITHEXPR -> ADDOP TERM RIGHTRECARITHEXPR .

RIGHTRECTERM -> .
RIGHTRECTERM -> MULTOP FACTOR RIGHTRECTERM .

SIGN -> plus .
SIGN -> minus .

STATBLOCK -> lcurbr REPTSTATBLOCK1 rcurbr .
STATBLOCK -> STATEMENT .
STATBLOCK -> .

STATEMENT -> id STATEMENTTAIL .
STATEMENT -> if lpar RELEXPR rpar then STATBLOCK else STATBLOCK semi .
STATEMENT -> while lpar RELEXPR rpar STATBLOCK semi .
STATEMENT -> read lpar VARIABLE rpar semi .
STATEMENT -> write lpar EXPR rpar semi .
STATEMENT -> return lpar EXPR rpar semi .

STATEMENTTAIL -> INDICE REPTINDICE STATEMENTBRACK .
STATEMENTTAIL -> ASSIGNEND .
STATEMENTTAIL -> POINTNESTR STATEMENTPOINTNEST .
STATEMENTTAIL -> PARAMS STATEMENTPARENT .

STATEMENTPARENT -> POINTNESTR STATEMENTPOINTNEST .
STATEMENTPARENT -> semi .

STATEMENTPOINTNEST -> REPTINDICE ASSIGNEND .
STATEMENTPOINTNEST -> ENDPARAMS .

STATEMENTBRACK -> ASSIGNEND .
STATEMENTBRACK -> POINTNESTR STATEMENTPOINTNEST .

ENDPARAMS -> PARAMS semi .

VARIABLEBRACK -> .
VARIABLEBRACK -> POINTNESTR REPTINDICE .

VARIABLETAIL -> INDICE REPTINDICE VARIABLEBRACK .
VARIABLETAIL -> .
VARIABLETAIL -> POINTNESTR REPTINDICE .
VARIABLETAIL -> PARAMS POINTNESTR REPTINDICE .

REPTINDICE -> INDICE REPTINDICE .
REPTINDICE -> .

POINTNESTR -> dot REPTIDNESTR id.

REPTIDNESTR -> id REPTIDNESTRTAIL .

REPTIDNESTRTAIL -> .
REPTIDNESTRTAIL -> INDICE REPTINDICE POINTNESTR .
REPTIDNESTRTAIL -> POINTNESTR .
REPTIDNESTRTAIL -> PARAMS POINTNESTR .

IDNESTTAIL -> INDICE REPTINDICE dot .
IDNESTTAIL -> dot .
IDNESTTAIL -> PARAMS dot .

TERM -> FACTOR RIGHTRECTERM .

TYPE -> integer .
TYPE -> float .
TYPE -> id .

VARIABLE -> id VARIABLETAIL .

VISIBILITY -> public .
VISIBILITY -> private .
VISIBILITY -> .
