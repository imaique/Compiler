START -> PROG eof  .

ADDOP -> plus  .
ADDOP -> minus  .
ADDOP -> or  .

APARAMS -> EXPR REPTAPARAMS1  .
APARAMS ->  .

APARAMSTAIL -> comma EXPR  .

ARITHEXPR -> TERM RIGHTRECARITHEXPR  .

ARRAYOROBJECT -> REPTARRAYSIZE  .
ARRAYOROBJECT -> APARAMSLIST  .

ARRAYSIZE -> lsqbr ARRAYSIZE2  .

ARRAYSIZE2 -> INTNUM rsqbr  .
ARRAYSIZE2 -> rsqbr  .

INTNUM -> intnum  .

ASSIGNOP -> assign  .

CLASSDECL -> class ID OPTINHERITS lcurbr REPTMEMBERDECL rcurbr semi  .

CLASSDECLORFUNCDEF -> CLASSDECL  .
CLASSDECLORFUNCDEF -> FUNCDEF  .

EXPR -> ARITHEXPR EXPR2  .

EXPR2 -> RELOPOP ARITHEXPR  .
EXPR2 ->  .

FACTOR -> ID FACTOR2 REPTVARIABLEORFUNCTIONCALL  .
FACTOR -> INTNUM  .
FACTOR -> floatnum  .
FACTOR -> lpar ARITHEXPR rpar  .
FACTOR -> not FACTOR  .
FACTOR -> SIGN FACTOR  .

FACTOR2 -> APARAMSLIST  .
FACTOR2 -> REPTIDNEST1  .

FPARAMS -> VARDECL REPTARRAYSIZE REPTFPARAMS4  .
FPARAMS ->  .

FPARAMSTAIL -> comma VARDECL REPTARRAYSIZE  .

FUNCBODY -> lcurbr REPTLOCALVARORSTAT rcurbr  .

FUNCDEF -> FUNCHEAD FUNCBODY  .

FUNCHEAD -> function ID FUNCHEADTAIL  .

PARAMLIST -> lpar FPARAMS rpar  .

FUNCHEADMEMBERTAIL -> ID PARAMLIST ARROWRETURNTYPE  .
FUNCHEADMEMBERTAIL -> constructorkeyword PARAMLIST  .

FUNCHEADTAIL -> sr FUNCHEADMEMBERTAIL  .
FUNCHEADTAIL -> PARAMLIST ARROWRETURNTYPE  .

ARROWRETURNTYPE -> arrow RETURNTYPE  .

IDNEST -> dot ID IDNEST2  .

IDNEST2 -> APARAMSLIST  .
IDNEST2 -> REPTIDNEST1  .

INDICE -> lsqbr ARITHEXPR rsqbr  .

LOCALVARDECL -> localvar VARDECL ARRAYOROBJECT semi  .

LOCALVARORSTAT -> LOCALVARDECL  .
LOCALVARORSTAT -> STATEMENT  .

MEMBERDECL -> MEMBERFUNCDECL  .
MEMBERDECL -> MEMBERVARDECL  .

MEMBERFUNCDECL -> MEMBERFUNCHEAD semi  .

MEMBERFUNCHEAD -> function ID colon PARAMLIST ARROWRETURNTYPE  .
MEMBERFUNCHEAD -> constructorkeyword colon PARAMLIST  .

MEMBERVARDECL -> attribute VARDECL REPTARRAYSIZE semi  .

ID -> id .

VARDECL -> ID colon TYPE .

MULTOP -> mult  .
MULTOP -> div  .
MULTOP -> and  .

OPTINHERITS -> isa ID REPTINHERITSLIST  .
OPTINHERITS ->  .

PROG -> REPTPROG0  .

RELEXPR -> ARITHEXPR RELOPOP ARITHEXPR  .

RELOPOP -> RELOP  .

RELOP -> eq  .
RELOP -> neq  .
RELOP -> lt  .
RELOP -> gt  .
RELOP -> leq  .
RELOP -> geq  .

REPTAPARAMS1 -> APARAMSTAIL REPTAPARAMS1  .
REPTAPARAMS1 ->  .

REPTARRAYSIZE -> ARRAYSIZE REPTARRAYSIZE  .
REPTARRAYSIZE ->  .

REPTFPARAMS4 -> FPARAMSTAIL REPTFPARAMS4  .
REPTFPARAMS4 ->  .

REPTIDNEST1 -> INDICE REPTIDNEST1  .
REPTIDNEST1 ->  .

REPTINHERITSLIST -> comma ID REPTINHERITSLIST  .
REPTINHERITSLIST ->  .

REPTLOCALVARORSTAT -> LOCALVARORSTAT REPTLOCALVARORSTAT  .
REPTLOCALVARORSTAT ->  .

REPTMEMBERDECL -> VISIBILITY MEMBERDECL REPTMEMBERDECL  .
REPTMEMBERDECL ->  .

REPTPROG0 -> CLASSDECLORFUNCDEF REPTPROG0  .
REPTPROG0 ->  .

REPTSTATBLOCK1 -> STATEMENT REPTSTATBLOCK1  .
REPTSTATBLOCK1 ->  .

REPTVARIABLE -> VARIDNEST REPTVARIABLE  .
REPTVARIABLE ->  .

REPTVARIABLEORFUNCTIONCALL -> IDNEST REPTVARIABLEORFUNCTIONCALL  .
REPTVARIABLEORFUNCTIONCALL ->  .

RETURNTYPE -> TYPE  .
RETURNTYPE -> void  .

RIGHTRECARITHEXPR -> ADDOP TERM RIGHTRECARITHEXPR  .
RIGHTRECARITHEXPR ->  .

RIGHTRECTERM -> MULTOP FACTOR RIGHTRECTERM  .
RIGHTRECTERM ->  .

SIGN -> plus  .
SIGN -> minus  .

STATBLOCK -> lcurbr REPTSTATBLOCK1 rcurbr  .
STATBLOCK -> STATEMENT  .
STATBLOCK ->  .

STATEMENTBLOCK -> STATBLOCK  .

STATEMENT -> ID STATEMENTIDNEST semi  .
STATEMENT -> if RELEXPRPAR then STATEMENTBLOCK else STATEMENTBLOCK semi  .
STATEMENT -> while RELEXPRPAR STATEMENTBLOCK semi  .
STATEMENT -> read lpar VARIABLE rpar semi  .
STATEMENT -> write EXPRPAR semi  .
STATEMENT -> return EXPRPAR semi  .

RELEXPRPAR -> lpar RELEXPR rpar .

EXPRPAR -> lpar EXPR rpar  .

STATEMENTIDNEST -> DOTIDSTATEMENTIDNEST  .
STATEMENTIDNEST -> APARAMSLIST STATEMENTIDNEST2  .
STATEMENTIDNEST -> INDICE REPTIDNEST1 STATEMENTIDNEST3  .
STATEMENTIDNEST -> ASSIGNOP EXPR  .

STATEMENTIDNEST2 ->  .
STATEMENTIDNEST2 -> DOTIDSTATEMENTIDNEST  .

STATEMENTIDNEST3 -> ASSIGNOP EXPR  .
STATEMENTIDNEST3 -> DOTIDSTATEMENTIDNEST  .

DOTIDSTATEMENTIDNEST -> dot ID STATEMENTIDNEST  .

TERM -> FACTOR RIGHTRECTERM  .

TYPE -> integer  .
TYPE -> float  .
TYPE -> id  .

VARIABLE -> ID VARIABLE2  .

VARIABLE2 -> REPTIDNEST1 REPTVARIABLE  .
VARIABLE2 -> APARAMSLISTIDNEST  .

VARIDNEST -> dot ID VARIDNEST2  .

APARAMSLIST -> lpar APARAMS rpar .

APARAMSLISTIDNEST -> APARAMSLIST VARIDNEST  .

VARIDNEST2 -> APARAMSLISTIDNEST  .
VARIDNEST2 -> REPTIDNEST1  .

VISIBILITY -> public  .
VISIBILITY -> private  .
